
#Область о //Стандартные процедуры и функции:

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если ПустаяСтрока(Параметры.АдресПараметровВХранилище) Тогда
		Отказ = Истина;
		Возврат;
	КонецЕсли;

	ПараметрыРасчета = ПолучитьИзВременногоХранилища(Параметры.АдресПараметровВХранилище);
	
	Сотрудник = ПараметрыРасчета.Сотрудник;
	ОбъектРасчетаЗарплаты = ПараметрыРасчета.ОбъектРасчетаЗарплаты;
	
	тзПрочиеУдержания.Очистить();
	стрПоиск = Новый Структура("Сотрудник",Сотрудник);
	массПрочиеУдержания = ОбъектРасчетаЗарплаты.ПрочиеУдержания.НайтиСтроки(стрПоиск); 
	Для каждого стрПрочиеУдержания из массПрочиеУдержания Цикл
		стртзПрочиеУдержания = тзПрочиеУдержания.Добавить();
		ЗаполнитьЗначенияСвойств(стртзПрочиеУдержания,стрПрочиеУдержания);		
	КонецЦикла;
	
	ЭтотОбъект.Заголовок = "Прочие удержания: "+Сотрудник.ФИОСокращенное

КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка)
	
	Если ЗавершениеРаботы И Модифицированность Тогда
		Отказ = Истина;
		Возврат;
	КонецЕсли;
	
	Если Модифицированность Тогда
		
		Модифицированность = Ложь;
		Закрыть(ПоместитьИзмененныеДанныеВоВременноеХранилище());
		
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура тзПрочиеУдержанияПриИзменении(Элемент)
	                            
	Модифицированность = Истина;
	РасчитатьИтоговыеСуммы();
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	РасчитатьИтоговыеСуммы();
	
КонецПроцедуры

&НаКлиенте
Процедура РасчитатьИтоговыеСуммы()
	
	РазмерУдержанияИтог = 0;
	Для каждого Строка из тзПрочиеУдержания Цикл
		
		РазмерУдержанияИтог = РазмерУдержанияИтог + Строка.СуммаУдержания;
		
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#Область ПереносВОсновнуюФорму

&НаСервере
Функция ПоместитьИзмененныеДанныеВоВременноеХранилище()
	
	ВозвращаемыеСведения = Новый Структура;
	ВозвращаемыеСведения.Вставить("Сотрудник", Сотрудник);
	ВозвращаемыеСведения.Вставить("тзПрочиеУдержания", тзПрочиеУдержания.Выгрузить());
	
	Возврат ПоместитьВоВременноеХранилище(ВозвращаемыеСведения, Новый УникальныйИдентификатор);
	
КонецФункции

&НаКлиенте
Процедура тзПрочиеУдержанияСуммаПриИзменении(Элемент)

	ТекущиеДанные = Элементы.тзПрочиеУдержания.ТекущиеДанные;
	Если ТекущиеДанные <> Неопределено Тогда
		ТекущиеДанные.ПроцентОтБазы = 0;	
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура тзПрочиеУдержанияПроцентОтБазыПриИзменении(Элемент)

	ТекущиеДанные = Элементы.тзПрочиеУдержания.ТекущиеДанные;
	Если ТекущиеДанные <> Неопределено Тогда
		ТекущиеДанные.СуммаУдержания = 0;	
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти


