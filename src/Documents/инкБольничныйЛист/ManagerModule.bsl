	 
#Область о //Дополнительные процедуры и функции:

// Функция - Получить перечень больничных листов
//
// Параметры:
//  Параметры	 - структура - Параметры для запроса 
// 
// Возвращаемое значение:
//   - массив значений - массив ссылок документа "Больничный лист".
//
Функция ПолучитьПереченьБольничныхЛистов(Параметры = Неопределено) Экспорт
	
	Если Параметры = Неопределено Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	инкРеестрБольничныхЛистов.Регистратор КАК Регистратор,
		|	инкРеестрБольничныхЛистов.Сотрудник КАК Сотрудник,
		|	инкРеестрБольничныхЛистов.МесяцНачисления КАК МесяцНачисления,
		|	СУММА(инкРеестрБольничныхЛистов.Сумма) КАК Сумма
		|ИЗ
		|	РегистрСведений.инкРеестрБольничныхЛистов КАК инкРеестрБольничныхЛистов
		|ГДЕ
		|	инкРеестрБольничныхЛистов.Сотрудник = &Сотрудник
		|	И инкРеестрБольничныхЛистов.МесяцНачисления = &МесяцНачисления
		|
		|СГРУППИРОВАТЬ ПО
		|	инкРеестрБольничныхЛистов.Регистратор,
		|	инкРеестрБольничныхЛистов.Сотрудник,
		|	инкРеестрБольничныхЛистов.МесяцНачисления";
	
	Запрос.УстановитьПараметр("МесяцНачисления", Параметры.МесяцНачисления);
	Запрос.УстановитьПараметр("Сотрудник", Параметры.Сотрудник);
	
	РезультатЗапроса = Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Регистратор");
	
	Возврат РезультатЗапроса;	
	
КонецФункции

#КонецОбласти	 

#Область о //Печать: 

#Область о //Больничный лист (результат расчета):

Функция СформироватьБольничныйЛистРезультатРасчетаТабличныйДокумент(БольничныйЛистОбъект) Экспорт
	
	БольничныйЛистТабличныйДокумент = Новый ТабличныйДокумент;
	БольничныйЛистМакет = ПолучитьМакет("МакетБольничныйЛистРезультатРасчета");
	
	// ЗаголовокБольничногоЛиста:
	ОбластьМакета = БольничныйЛистМакет.ПолучитьОбласть("ЗаголовокБольничногоЛиста");
	ЗаголовокТекст = БольничныйЛистОбъект.Сотрудник.ФИОСокращенное  
				   + " - б/лист  № " + БольничныйЛистОбъект.БольничныйЛистНомер    
				   + " с " + Формат(БольничныйЛистОбъект.БольничныйДатаНачала,"ДФ=dd.MM.yyyy") 
				   + " по " + Формат(БольничныйЛистОбъект.БольничныйДатаОкончания,"ДФ=dd.MM.yyyy")
				   + " за " + Формат(БольничныйЛистОбъект.МесяцНачисления,"ДФ='ММММ гггг'") + " г.";
	ОбластьМакета.Параметры.ЗаголовокБольничногоЛиста = ЗаголовокТекст;
	БольничныйЛистТабличныйДокумент.Вывести(ОбластьМакета);
	
	// ДанныеСотрудника:
	ОбластьМакета = БольничныйЛистМакет.ПолучитьОбласть("ДанныеСотрудника");
	ДанныеСотрудникаТекст = "ИНН: " + БольничныйЛистОбъект.Сотрудник.ИНН
	 					  + " СНИЛС: " + БольничныйЛистОбъект.Сотрудник.СНИЛС;	
	ОбластьМакета.Параметры.ДанныеСотрудника = ДанныеСотрудникаТекст;
	БольничныйЛистТабличныйДокумент.Вывести(ОбластьМакета);
	
	// Отступ:
	ОбластьМакета = БольничныйЛистМакет.ПолучитьОбласть("Отступ");
	БольничныйЛистТабличныйДокумент.Вывести(ОбластьМакета);

	// Расчетные данные:
	ПечатьРасчетныхДанных(БольничныйЛистТабличныйДокумент,БольничныйЛистМакет,БольничныйЛистОбъект);
	
	// Отступ:
	ОбластьМакета = БольничныйЛистМакет.ПолучитьОбласть("Отступ");
	БольничныйЛистТабличныйДокумент.Вывести(ОбластьМакета);
	
	// ПояснениеРасчета:
	ОбластьМакета = БольничныйЛистМакет.ПолучитьОбласть("ПояснениеРасчета");
	ОбластьМакета.Параметры.ПояснениеРасчета = БольничныйЛистОбъект.ПояснениеРасчета;
	БольничныйЛистТабличныйДокумент.Вывести(ОбластьМакета);
	
	БольничныйЛистТабличныйДокумент.ПолеСверху = 2;
	БольничныйЛистТабличныйДокумент.ПолеСнизу  = 2;
	БольничныйЛистТабличныйДокумент.ПолеСлева  = 23;
	БольничныйЛистТабличныйДокумент.ПолеСправа = 2;
	БольничныйЛистТабличныйДокумент.РазмерКолонтитулаСверху	= 2;
	БольничныйЛистТабличныйДокумент.РазмерКолонтитулаСнизу	= 2;
	БольничныйЛистТабличныйДокумент.МасштабПечати = 100;
    
	Возврат БольничныйЛистТабличныйДокумент;
	
КонецФункции

Процедура ПечатьРасчетныхДанных(БольничныйЛистТабличныйДокумент,БольничныйЛистМакет,БольничныйЛистОбъект);

	// РасчетныеДанныеЗаголовок:
	ОбластьМакета = БольничныйЛистМакет.ПолучитьОбласть("РасчетныеДанныеЗаголовок|РасчетныеДанныеТаблица");
	БольничныйЛистТабличныйДокумент.Вывести(ОбластьМакета);                                         
	ОбластьМакета = БольничныйЛистМакет.ПолучитьОбласть("РасчетныеДанныеЗаголовок|РасчетныеДанныеРазделитель");
	БольничныйЛистТабличныйДокумент.Присоединить(ОбластьМакета);                                         
	ОбластьМакета = БольничныйЛистМакет.ПолучитьОбласть("РасчетныеДанныеЗаголовок|РасчетныеДанныеТаблица");
	БольничныйЛистТабличныйДокумент.Присоединить(ОбластьМакета);                                         
	
	ПериодДата1 = НачалоГода(ДобавитьМесяц(БольничныйЛистОбъект.МесяцНачисления,-24));
	ПериодДата2 = НачалоГода(ДобавитьМесяц(БольничныйЛистОбъект.МесяцНачисления,-12));
	Для МесяцЧисло = 1 По 12 Цикл
		
		ОбластьМакета = БольничныйЛистМакет.ПолучитьОбласть("РасчетныеДанныеСтрока|РасчетныеДанныеТаблица");
		ЗаполнитьДанныеОбластиПоПериодуМесяц(ПериодДата1,БольничныйЛистОбъект.РасчетныеДанные,ОбластьМакета);
		БольничныйЛистТабличныйДокумент.Вывести(ОбластьМакета);
		
		ОбластьМакета = БольничныйЛистМакет.ПолучитьОбласть("РасчетныеДанныеСтрока|РасчетныеДанныеРазделитель");
		БольничныйЛистТабличныйДокумент.Присоединить(ОбластьМакета);                                         
		
		ОбластьМакета = БольничныйЛистМакет.ПолучитьОбласть("РасчетныеДанныеСтрока|РасчетныеДанныеТаблица");
		ЗаполнитьДанныеОбластиПоПериодуМесяц(ПериодДата2,БольничныйЛистОбъект.РасчетныеДанные,ОбластьМакета);
		БольничныйЛистТабличныйДокумент.Присоединить(ОбластьМакета);
		
		ПериодДата1 = ДобавитьМесяц(ПериодДата1,1);
		ПериодДата2 = ДобавитьМесяц(ПериодДата2,1);

	КонецЦикла;
	
	// РасчетныеДанныеИтоги:        
	ПериодДата1 = НачалоГода(ДобавитьМесяц(БольничныйЛистОбъект.МесяцНачисления,-24));
	ПериодДата2 = НачалоГода(ДобавитьМесяц(БольничныйЛистОбъект.МесяцНачисления,-12));

	ОбластьМакета = БольничныйЛистМакет.ПолучитьОбласть("РасчетныеДанныеИтоги|РасчетныеДанныеТаблица");
	ЗаполнитьДанныеОбластиПоПериоду(ПериодДата1,БольничныйЛистОбъект.РасчетныеДанные,ОбластьМакета);
	БольничныйЛистТабличныйДокумент.Вывести(ОбластьМакета);
	
	ОбластьМакета = БольничныйЛистМакет.ПолучитьОбласть("РасчетныеДанныеИтоги|РасчетныеДанныеРазделитель");
	БольничныйЛистТабличныйДокумент.Присоединить(ОбластьМакета);                                         
	
	ОбластьМакета = БольничныйЛистМакет.ПолучитьОбласть("РасчетныеДанныеИтоги|РасчетныеДанныеТаблица");
	ЗаполнитьДанныеОбластиПоПериоду(ПериодДата2,БольничныйЛистОбъект.РасчетныеДанные,ОбластьМакета);
	БольничныйЛистТабличныйДокумент.Присоединить(ОбластьМакета);

КонецПроцедуры                                

Процедура ЗаполнитьДанныеОбластиПоПериоду(ПериодДата,РасчетныеДанные,ОбластьМакета)
	
	РасчетныеДанныеСвод = РасчетныеДанные.Выгрузить().Скопировать();
	РасчетныеДанныеСвод.Очистить();

	ГодЧисло = Год(ПериодДата);
	Для каждого РасчетныеДанныеСтрока Из РасчетныеДанные Цикл
		
		Если РасчетныеДанныеСтрока.Год = ГодЧисло Тогда
			РасчетныеДанныеСводСтрока = РасчетныеДанныеСвод.Добавить();
			ЗаполнитьЗначенияСвойств(РасчетныеДанныеСводСтрока,РасчетныеДанныеСтрока);
		КонецЕсли;
		
	КонецЦикла;     
	
	РасчетныеДанныеСвод.Свернуть("Год","КоличествоДнейЧасовНорма,КоличествоДнейЧасовФакт,Зарплата,Итого");
	Для каждого РасчетныеДанныеСтрока Из РасчетныеДанныеСвод Цикл
  		ЗаполнитьЗначенияСвойств(ОбластьМакета.Параметры,РасчетныеДанныеСтрока);
	КонецЦикла;     
	
КонецПроцедуры

Процедура ЗаполнитьДанныеОбластиПоПериодуМесяц(ПериодДата,РасчетныеДанные,ОбластьМакета)
	
	ПоискСтруктура = Новый Структура("Месяц",ПериодДата);
	РасчетныеДанныеМассив = РасчетныеДанные.НайтиСтроки(ПоискСтруктура);
	Для каждого РасчетныеДанныеЭлемент Из РасчетныеДанныеМассив Цикл             	
		ЗаполнитьЗначенияСвойств(ОбластьМакета.Параметры,РасчетныеДанныеЭлемент); 
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти