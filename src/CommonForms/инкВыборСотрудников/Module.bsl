
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	УстановитьЗначениеРеквизитовПоУмолчаниюНаСервере();	
	ЗаполнитьСписокСотрудниковНаСервере();
	
КонецПроцедуры

&НаСервере
Процедура УстановитьЗначениеРеквизитовПоУмолчаниюНаСервере()
	
	Организация = Справочники.инкОрганизации.НайтиПоКоду("000000001"); // ООО "Конфетпром"
	ИсключитьУволенных = Ложь;
	ДатаИсключения = НачалоГода(ТекущаяДата()) - 1;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьСписокСотрудниковНаСервере()
	
	Если Не ЗначениеЗаполнено(Организация) Тогда
		Возврат;
	КонецЕсли;
	
	СотрудникиТаблица.Очистить();
	
	СтруктураПараметров = инкСотрудникиСервер.ПолучитьСтруктуруПараметровДляЗапроса_Сотрудники();
	СтруктураПараметров.Организация 	= Организация;
	СтруктураПараметров.Подразделение 	= Подразделение;
	РаботающиеСотрудникиТаблица = инкСотрудникиСервер.ПолучитьТаблицуЗначенийДляЗаполнения_Сотрудники(СтруктураПараметров);

    Для каждого РаботающийСотрудник Из РаботающиеСотрудникиТаблица Цикл
        
        ДобавитьЗапись = Истина;
		Если ИсключитьУволенных Тогда
			
			Если ЗначениеЗаполнено(РаботающийСотрудник.ДатаУвольнения) Тогда
				Если РаботающийСотрудник.ДатаУвольнения < ДатаИсключения Тогда
					ДобавитьЗапись = Ложь;
				КонецЕсли;
			КонецЕсли;
			
		КонецЕсли;
		
		Если ДобавитьЗапись Тогда
			СотрудникиСтрока = СотрудникиТаблица.Добавить();
			СотрудникиСтрока.Сотрудник = РаботающийСотрудник.Сотрудник;
		КонецЕсли;
    	
    КонецЦикла; 
	
КонецПроцедуры

&НаСервере
Процедура ОрганизацияПриИзмененииНаСервере()
	
	Подразделение = Справочники.инкПодразделения.ПустаяСсылка();
	ЗаполнитьСписокСотрудниковНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура ОрганизацияПриИзменении(Элемент)
	
	ОрганизацияПриИзмененииНаСервере();
		
КонецПроцедуры

&НаКлиенте
Процедура УстановитьОтметкиВТаблице(Отметка)
	
	Для каждого СотрудникСтрока Из СотрудникиТаблица Цикл
		СотрудникСтрока.ВыборСтроки = Отметка;
	КонецЦикла;

КонецПроцедуры

&НаКлиенте
Процедура ВыделитьВсе(Команда)
	
	УстановитьОтметкиВТаблице(Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура СнятьОтметки(Команда)
	
	УстановитьОтметкиВТаблице(Ложь);
	
КонецПроцедуры

&НаКлиенте
Процедура ИнвертироватьВыделение(Команда)
	
	Для каждого СотрудникСтрока Из СотрудникиТаблица Цикл
		СотрудникСтрока.ВыборСтроки = Не (СотрудникСтрока.ВыборСтроки);
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура ИсключитьУволенныхПриИзменении(Элемент)
	
	Элементы.ДатаИсключения.Доступность = ИсключитьУволенных; 
	ЗаполнитьСписокСотрудниковНаСервере();
	
КонецПроцедуры

&НаСервере
Процедура ПодразделениеПриИзмененииНаСервере()
	
	ЗаполнитьСписокСотрудниковНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура ПодразделениеПриИзменении(Элемент)
	
	ПодразделениеПриИзмененииНаСервере();
	
КонецПроцедуры

&НаСервере
Процедура ДатаИсключенияПриИзмененииНаСервере()
	
	ЗаполнитьСписокСотрудниковНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура ДатаИсключенияПриИзменении(Элемент)
	
	ДатаИсключенияПриИзмененииНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура ФормаОК(Команда)
	
	ПоискВыбор = Новый Структура("ВыборСтроки",Истина);
	СотрудникиМассив = СотрудникиТаблица.НайтиСтроки(ПоискВыбор);
	
	РезультатВыбора = Новый Массив;
	Для каждого СотрудникСтрока из СотрудникиМассив Цикл
		РезультатВыбора.Добавить(СотрудникСтрока.Сотрудник);	
	КонецЦикла;
	
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("СотрудникиМассив",РезультатВыбора);
	ДополнительныеПараметры.Вставить("Организация",Организация);
	ДополнительныеПараметры.Вставить("Подразделение",Подразделение);
	
	ЭтаФорма.Закрыть(ДополнительныеПараметры);
	
КонецПроцедуры






