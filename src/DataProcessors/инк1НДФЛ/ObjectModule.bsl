
#Область о // Объявление переменных

Перем СотрудникиМассив Экспорт;
Перем Организация Экспорт;

#КонецОбласти 

#Область о // Печать расчетных листков

Функция ПолучитьРезультатРасчета() Экспорт
	
	ТабличныйДокумент = Получить1НДФЛНаСервере();
	Возврат ТабличныйДокумент; 
	
КонецФункции

#КонецОбласти

#Область о // Загрузка данных в табличный документ

Функция Получить1НДФЛНаСервере()
	
	Если Не ЗначениеЗаполнено(ЭтотОбъект.Год) Тогда
		Сообщить("Не задан год.");
		Возврат Неопределено;
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(Организация) Тогда
		Сообщить("Не задана организация.");
		Возврат Неопределено;
	КонецЕсли;
	
	ТабДок = Новый ТабличныйДокумент;
	Макет = ПолучитьМакет("инкПФ_MXL_ФормаРегистрНалоговогоУчетаПоНДФЛ");
	ОблагаемыеДоходыТаблица 				= Получить_ОблагаемыеДоходы(СотрудникиМассив);
	ВычетыЗаИсключениемСтандартныхТаблица 	= Получить_ВычетыЗаИсключениемСтандартных(СотрудникиМассив);
	СтандартныеНалоговыеВычетыТаблица 		= Получить_СтандартныеНалоговыеВычеты(СотрудникиМассив);
	НалогУдержанныйТаблица 					= Получить_НалогУдержанный(СотрудникиМассив);
	
	Для каждого СотрудникСтрока из СотрудникиМассив Цикл 
		
		ПоискСотрудника = Новый Структура("Сотрудник",СотрудникСтрока);
		ОблагаемыеДоходы = ОблагаемыеДоходыТаблица.НайтиСтроки(ПоискСотрудника);
		ВычетыЗаИсключениемСтандартных	= ВычетыЗаИсключениемСтандартныхТаблица.НайтиСтроки(ПоискСотрудника);
		СтандартныеНалоговыеВычеты      = СтандартныеНалоговыеВычетыТаблица.НайтиСтроки(ПоискСотрудника);
		НалогУдержанный					= НалогУдержанныйТаблица.НайтиСтроки(ПоискСотрудника);
		
		Если ОблагаемыеДоходы.Количество() = 0 Тогда
			Продолжить;
		КонецЕсли;			
		
		ВывестиРаздел1(ТабДок,Макет);
		ВывестиРаздел2(СотрудникСтрока,ТабДок,Макет);

		ТабДок.ВывестиГоризонтальныйРазделительСтраниц();
		
		// Раздел 3. РАСЧЕТ НАЛОГОВОЙ БАЗЫ И НАЛОГА НА ДОХОДЫ ФИЗИЧЕСКОГО ЛИЦА
		ВывестиРаздел3_Заголовок(СотрудникСтрока,ТабДок,Макет);
		ВывестиРаздел3_Доходы(ОблагаемыеДоходы,ТабДок,Макет);
		ВывестиРаздел3_ВычетыЗаИсключениемСтандартных(ВычетыЗаИсключениемСтандартных,ТабДок,Макет);
		ВывестиРаздел3_Раздел3ДоходыЗаИсключениемВычетов(ОблагаемыеДоходы,ВычетыЗаИсключениемСтандартных,ТабДок,Макет);
		ВывестиРаздел3_СтандартныеНалоговыеВычеты(СтандартныеНалоговыеВычеты,ТабДок,Макет);
		ВывестиРаздел3_ОбщиеИтоги(СотрудникСтрока,ОблагаемыеДоходы,ВычетыЗаИсключениемСтандартных,СтандартныеНалоговыеВычеты,НалогУдержанный,ТабДок,Макет);
		ВывестиРаздел3_ПлатежноеПоручение(ТабДок,Макет);
		ВывестиРаздел3_Подвал(ТабДок,Макет);

		ТабДок.ОриентацияСтраницы = ОриентацияСтраницы.Ландшафт;
		ТабДок.ВывестиГоризонтальныйРазделительСтраниц();
		
	КонецЦикла;
	
	инкОтчетыСервер.УстановитьНастройкиМаштабаДокумента(ТабДок);

	Возврат ТабДок;
	
КонецФункции

Функция Получить_ОблагаемыеДоходы(СотрудникиМассив)
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	МЕСЯЦ(инкДоходыНДФЛОбороты.Период) КАК МесяцНалоговогоПериода,
		|	инкДоходыНДФЛОбороты.Сотрудник КАК Сотрудник,
		|	инкДоходыНДФЛОбороты.ДоходНДФЛ КАК КодДоходаСтрокой,
		|	инкДоходыНДФЛОбороты.СуммаНачисленияОборот КАК СуммаДохода
		|ИЗ
		|	РегистрНакопления.инкНачисления.Обороты(
		|			&Дата1,
		|			&Дата2,
		|			Месяц,
		|			Сотрудник В (&СотрудникиМассив)
		|				И Налог = ИСТИНА) КАК инкДоходыНДФЛОбороты
		|
		|УПОРЯДОЧИТЬ ПО
		|	Сотрудник,
		|	МесяцНалоговогоПериода,
		|	КодДоходаСтрокой";
	
	Если Не ЗначениеЗаполнено(СотрудникиМассив) Тогда
		
		Запрос.Текст = СтрЗаменить(Запрос.Текст,"Сотрудник В (&СотрудникиМассив)","Истина");
		
	КонецЕсли;
	
	Запрос.УстановитьПараметр("Дата1", НачалоГода(ЭтотОбъект.Год));
	Запрос.УстановитьПараметр("Дата2", КонецГода(ЭтотОбъект.Год));
	Запрос.УстановитьПараметр("СотрудникиМассив", СотрудникиМассив);
	
	тз = Запрос.Выполнить().Выгрузить();
	тз.Индексы.Добавить("Сотрудник");
	
	Возврат тз;
	
КонецФункции

Функция Получить_ВычетыЗаИсключениемСтандартных(СотрудникиМассив)
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	МЕСЯЦ(инкДоходыНДФЛОбороты.Период) КАК МесяцНалоговогоПериода,
		|	инкДоходыНДФЛОбороты.Сотрудник КАК Сотрудник,
		|	инкДоходыНДФЛОбороты.ВычетНДФЛ КАК КодВычетаСтрокой,
		|	инкДоходыНДФЛОбороты.СуммаВычетаОборот КАК СуммаВычета
		|ПОМЕСТИТЬ втСкидки
		|ИЗ
		|	РегистрНакопления.инкНачисления.Обороты(
		|			&Дата1,
		|			&Дата2,
		|			Месяц,
		|			Сотрудник В (&СотрудникиМассив)
		|				И Налог = ИСТИНА) КАК инкДоходыНДФЛОбороты
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	МЕСЯЦ(инкВычетыНДФЛОбороты.Период) КАК МесяцНалоговогоПериода,
		|	инкВычетыНДФЛОбороты.Сотрудник КАК Сотрудник,
		|	инкВычетыНДФЛОбороты.ВычетНДФЛ КАК ВычетНДФЛ,
		|	инкВычетыНДФЛОбороты.СуммаВычетаОборот КАК СуммаВычетаОборот
		|ПОМЕСТИТЬ втВычеты
		|ИЗ
		|	РегистрНакопления.инкВычетыНДФЛ.Обороты(
		|			&Дата1,
		|			&Дата2,
		|			Месяц,
		|			Сотрудник В (&СотрудникиМассив)
		|				И НЕ ГруппаВычетаНДФЛ В (&СтандартныеВычеты)) КАК инкВычетыНДФЛОбороты
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	втСкидки.МесяцНалоговогоПериода КАК МесяцНалоговогоПериода,
		|	втСкидки.Сотрудник КАК Сотрудник,
		|	втСкидки.КодВычетаСтрокой КАК КодВычетаСтрокой,
		|	втСкидки.СуммаВычета КАК СуммаВычета
		|ПОМЕСТИТЬ втОбъединение
		|ИЗ
		|	втСкидки КАК втСкидки
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	втВычеты.МесяцНалоговогоПериода,
		|	втВычеты.Сотрудник,
		|	втВычеты.ВычетНДФЛ,
		|	втВычеты.СуммаВычетаОборот
		|ИЗ
		|	втВычеты КАК втВычеты
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	втОбъединение.МесяцНалоговогоПериода КАК МесяцНалоговогоПериода,
		|	втОбъединение.Сотрудник КАК Сотрудник,
		|	втОбъединение.КодВычетаСтрокой КАК КодВычетаСтрокой,
		|	СУММА(втОбъединение.СуммаВычета) КАК СуммаВычета
		|ИЗ
		|	втОбъединение КАК втОбъединение
		|
		|СГРУППИРОВАТЬ ПО
		|	втОбъединение.МесяцНалоговогоПериода,
		|	втОбъединение.Сотрудник,
		|	втОбъединение.КодВычетаСтрокой
		|
		|УПОРЯДОЧИТЬ ПО
		|	Сотрудник,
		|	МесяцНалоговогоПериода,
		|	КодВычетаСтрокой";
	
	Если Не ЗначениеЗаполнено(СотрудникиМассив) Тогда
		
		Запрос.Текст = СтрЗаменить(Запрос.Текст,"Сотрудник В (&СотрудникиМассив)","Истина");
		
	КонецЕсли;
	
	Запрос.УстановитьПараметр("Дата1", НачалоГода(ЭтотОбъект.Год));
	Запрос.УстановитьПараметр("Дата2", КонецГода(ЭтотОбъект.Год));
	Запрос.УстановитьПараметр("СотрудникиМассив", СотрудникиМассив);
	СтандартныеВычеты = инкНалогСервер.ПолучитьМассивГруппСтандартныхВычетов();
	Запрос.УстановитьПараметр("СтандартныеВычеты", СтандартныеВычеты);
	
	тз = Запрос.Выполнить().Выгрузить();
	тз.Индексы.Добавить("Сотрудник");
	
	Возврат тз;
	
КонецФункции

Функция Получить_СтандартныеНалоговыеВычеты(СотрудникиМассив)
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	МЕСЯЦ(инкВычетыНДФЛОбороты.Период) КАК МесяцНалоговогоПериода,
		|	инкВычетыНДФЛОбороты.Сотрудник КАК Сотрудник,
		|	инкВычетыНДФЛОбороты.ВычетНДФЛ КАК КодВычетаСтрокой,
		|	инкВычетыНДФЛОбороты.СуммаВычетаОборот КАК СуммаВычета
		|ИЗ
		|	РегистрНакопления.инкВычетыНДФЛ.Обороты(
		|			&Дата1,
		|			&Дата2,
		|			Месяц,
		|			Сотрудник В (&СотрудникиМассив)
		|				И ГруппаВычетаНДФЛ В (&СтандартныеВычеты)) КАК инкВычетыНДФЛОбороты
		|
		|УПОРЯДОЧИТЬ ПО
		|	Сотрудник,
		|	КодВычетаСтрокой";
	
	Если Не ЗначениеЗаполнено(СотрудникиМассив) Тогда
		
		Запрос.Текст = СтрЗаменить(Запрос.Текст,"Сотрудник В (&СотрудникиМассив)","Истина");
		
	КонецЕсли;
	
	Запрос.УстановитьПараметр("Дата1", НачалоГода(ЭтотОбъект.Год));
	Запрос.УстановитьПараметр("Дата2", КонецГода(ЭтотОбъект.Год));
	Запрос.УстановитьПараметр("СотрудникиМассив", СотрудникиМассив);
	СтандартныеВычеты = инкНалогСервер.ПолучитьМассивГруппСтандартныхВычетов();
	Запрос.УстановитьПараметр("СтандартныеВычеты", СтандартныеВычеты);
	
	тз = Запрос.Выполнить().Выгрузить();
	тз.Индексы.Добавить("Сотрудник");
	
	Возврат тз;
	
КонецФункции

Функция Получить_НалогУдержанный(СотрудникиМассив)
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	МЕСЯЦ(инкНалогУдержанныйОбороты.Период) КАК МесяцНалоговогоПериода,
		|	инкНалогУдержанныйОбороты.Сотрудник КАК Сотрудник,
		|	инкНалогУдержанныйОбороты.НалоговаяСтавка КАК НалоговаяСтавка,
		|	инкНалогУдержанныйОбороты.СуммаНалогаОборот КАК СуммаНалога
		|ИЗ
		|	РегистрНакопления.инкНалогУдержанный.Обороты(&Дата1, &Дата2, Месяц, Сотрудник В (&СотрудникиМассив)) КАК инкНалогУдержанныйОбороты
		|
		|УПОРЯДОЧИТЬ ПО
		|	Сотрудник,
		|	НалоговаяСтавка";
	
	Если Не ЗначениеЗаполнено(СотрудникиМассив) Тогда
		
		Запрос.Текст = СтрЗаменить(Запрос.Текст,"Сотрудник В (&СотрудникиМассив)","Истина");
		
	КонецЕсли;
	
	Запрос.УстановитьПараметр("Дата1", НачалоГода(ЭтотОбъект.Год));
	Запрос.УстановитьПараметр("Дата2", КонецГода(ЭтотОбъект.Год));
	Запрос.УстановитьПараметр("СотрудникиМассив", СотрудникиМассив);
	
	тз = Запрос.Выполнить().Выгрузить();
	тз.Индексы.Добавить("Сотрудник");
	
	Возврат тз;
	
КонецФункции

Процедура ЗаполнитьПолеВычета(ИндексВычеты,ВычетыКоличество,Вычет,ВычетыТаблица)
	
	Если ИндексВычеты > ВычетыКоличество Тогда
		Возврат;
	КонецЕсли;
	
	ЗаполнитьЗначенияСвойств(Вычет.Параметры,ВычетыТаблица[ИндексВычеты]);	
	ИндексВычеты = ИндексВычеты+ 1;
	
КонецПроцедуры

Функция РассчитатьОбщиеСуммыДоходаИНалога(ОблагаемыеДоходы,Вычеты,НалогУдержанный,Процент = 0.13)
	
	СтруктураДоходаИНалога = Новый Структура;
	
	СтруктураДоходаИНалога.Вставить("ОбщаяСуммаДохода",0);
	СтруктураДоходаИНалога.Вставить("ОбщаяСуммаВычетов",0);
	СтруктураДоходаИНалога.Вставить("ОблагаемаяСуммаДохода",0);
	СтруктураДоходаИНалога.Вставить("Исчислено",0);
	СтруктураДоходаИНалога.Вставить("ЗачтеноАвансовыхПлатежей",0);
	
	СтруктураДоходаИНалога.Вставить("Удержано",0);
	СтруктураДоходаИНалога.Вставить("Перечислено",0);
	СтруктураДоходаИНалога.Вставить("ИзлишнеУдержано",0);
	СтруктураДоходаИНалога.Вставить("Задолженность",0);
	
	Для каждого ОблагаемыеДоходыСтрока из ОблагаемыеДоходы Цикл
		СтруктураДоходаИНалога.ОбщаяСуммаДохода  = СтруктураДоходаИНалога.ОбщаяСуммаДохода
		                                         + ОблагаемыеДоходыСтрока.СуммаДохода;
		СтруктураДоходаИНалога.ОбщаяСуммаВычетов = СтруктураДоходаИНалога.ОбщаяСуммаВычетов
		                                         + ОблагаемыеДоходыСтрока.СуммаВычета;
	КонецЦикла;
	
	Для каждого ВычетыСтрока из Вычеты Цикл
		СтруктураДоходаИНалога.ОбщаяСуммаВычетов = СтруктураДоходаИНалога.ОбщаяСуммаВычетов
		                                         + ВычетыСтрока.СуммаВычета;
	КонецЦикла;

	Для каждого НалогУдержанныйСтрока из НалогУдержанный Цикл
		СтруктураДоходаИНалога.Удержано = СтруктураДоходаИНалога.Удержано
		                                + НалогУдержанныйСтрока.СуммаНалога;
	КонецЦикла;
	
	СтруктураДоходаИНалога.ОблагаемаяСуммаДохода = СтруктураДоходаИНалога.ОбщаяСуммаДохода
	                                             - СтруктураДоходаИНалога.ОбщаяСуммаВычетов; 
												 
	СтруктураДоходаИНалога.Исчислено = СтруктураДоходаИНалога.ОблагаемаяСуммаДохода * Процент; 
												 
	Возврат СтруктураДоходаИНалога;
	
КонецФункции

Процедура ВывестиРаздел1(ТабДок,Макет)
		
	Раздел0 = Макет.ПолучитьОбласть("Раздел0");
	ТабДок.Вывести(Раздел0);
	
	Раздел1 = Макет.ПолучитьОбласть("Раздел1");
	Раздел1.Параметры.ГодНП					= Год(ЭтотОбъект.Год);
	Раздел1.Параметры.ИНН                   = Организация.ИНН;
	Раздел1.Параметры.КодНалоговогоОргана   = Организация.КодНалоговогоОргана;
	Раздел1.Параметры.НазваниеОрганизации   = Организация.Наименование;

	ТабДок.Вывести(Раздел1);
		
КонецПроцедуры		
		
Процедура ВывестиРаздел2(СотрудникСтрока,ТабДок,Макет)
		
	Раздел2 = Макет.ПолучитьОбласть("Раздел2");
	Раздел2.Параметры.ИНН					= СотрудникСтрока.ИНН;
	Раздел2.Параметры.ФИО                   = СотрудникСтрока.ФИО;
	Раздел2.Параметры.КодДокумента          = СотрудникСтрока.ДокументУдостоверяющийЛичность.КодМВД;
	Раздел2.Параметры.СерияНомерДокумента   = СотрудникСтрока.ДокументСерия;
	Раздел2.Параметры.ДатаРождения          = СотрудникСтрока.ДокументНомер;
	// Сделать после привязки адресного классификатора:
	//КодСтраны
	//Индекс
	//Регион
	//Район
	//Город
	//НаселенныйПункт
	//Улица
	//Дом
	//Корпус
	//Квартира
	//СтранаПроживания
	//Адрес

	ТабДок.Вывести(Раздел2);
	
КонецПроцедуры

Процедура ВывестиРаздел3_Заголовок(СотрудникСтрока,ТабДок,Макет)

	// Заголовок:
	инкРаздел3 = Макет.ПолучитьОбласть("инкРаздел3");
	инкРаздел3.Параметры.ФИО = СотрудникСтрока.ФИО;
	ТабДок.Вывести(инкРаздел3);

	НаименованиеПоказателей = Макет.ПолучитьОбласть("инкРаздел3ЗаголовокТаблицы|НаименованиеПоказателей");
  	ТабДок.Вывести(НаименованиеПоказателей);
	//
	ТекущийМесяц = НачалоГода(ЭтотОбъект.Год);
	Для НомерМесяца = 1 по 12 Цикл
		
		МесяцТаблица = Макет.ПолучитьОбласть("инкРаздел3ЗаголовокТаблицы|МесяцТаблица");
		МесяцТаблица.Параметры.Месяц = ТекущийМесяц;
  		ТабДок.Присоединить(МесяцТаблица);

		ТекущийМесяц = ДобавитьМесяц(ТекущийМесяц,1);
		
	КонецЦикла;
	//
	ИтогТаблица = Макет.ПолучитьОбласть("инкРаздел3ЗаголовокТаблицы|ИтогТаблица");
  	ТабДок.Присоединить(ИтогТаблица);

КонецПроцедуры

Процедура ВывестиРаздел3_Доходы(ОблагаемыеДоходы,ТабДок,Макет)
	
	// Доходы:
	ДоходыСоответсвие = Новый Соответствие;
	Для каждого ОблагаемыеДоходыЭлемент из ОблагаемыеДоходы Цикл
		ДоходыСоответсвие.Вставить(ОблагаемыеДоходыЭлемент.КодДоходаСтрокой,ОблагаемыеДоходыЭлемент.КодДоходаСтрокой);	
	КонецЦикла;
	
	Для каждого ДоходыЭлемент из ДоходыСоответсвие Цикл
		
		ДоходИтогЧисло = 0;
		
		НаименованиеПоказателей = Макет.ПолучитьОбласть("инкРаздел3СтрокаДохода|НаименованиеПоказателей");
		НаименованиеПоказателей.Параметры.КодДохода = "Код "+ДоходыЭлемент.Значение;
		
		ТабДок.Вывести(НаименованиеПоказателей);
		
		//
		Для НомерМесяца = 1 по 12 Цикл
			
			ДоходЧисло = 0;
			Для каждого ОблагаемыеДоходыЭлемент из ОблагаемыеДоходы Цикл
				
				Если (НомерМесяца = ОблагаемыеДоходыЭлемент.МесяцНалоговогоПериода) И
					 (ДоходыЭлемент.Значение = ОблагаемыеДоходыЭлемент.КодДоходаСтрокой)
				Тогда
					ДоходЧисло = ДоходЧисло + ОблагаемыеДоходыЭлемент.СуммаДохода;
				КонецЕсли;
				
			КонецЦикла;
			
			МесяцТаблица = Макет.ПолучитьОбласть("инкРаздел3СтрокаДохода|МесяцТаблица");
			МесяцТаблица.Параметры.Доход = ДоходЧисло;
			ТабДок.Присоединить(МесяцТаблица);

			ДоходИтогЧисло = ДоходИтогЧисло + ДоходЧисло;

		КонецЦикла;
		//
		ИтогТаблица = Макет.ПолучитьОбласть("инкРаздел3СтрокаДохода|ИтогТаблица");
		ИтогТаблица.Параметры.ДоходИтог = ДоходИтогЧисло;
		ТабДок.Присоединить(ИтогТаблица);
		
	КонецЦикла;
	
КонецПроцедуры

Процедура ВывестиРаздел3_ВычетыЗаИсключениемСтандартных(ВычетыЗаИсключениемСтандартных,ТабДок,Макет)
	
	// Налоговые вычеты, за исключением стандартных:
	НалоговыеВычетыСоответсвие = Новый Соответствие;
	Для каждого ВычетыЭлемент из ВычетыЗаИсключениемСтандартных Цикл
		НалоговыеВычетыСоответсвие.Вставить(ВычетыЭлемент.КодВычетаСтрокой,ВычетыЭлемент.КодВычетаСтрокой);	
	КонецЦикла;
	
	// Наименование:
	НаименованиеПоказателей = Макет.ПолучитьОбласть("инкРаздел3ВычетыЗаИсключениемСтандартных|НаименованиеПоказателей");
	НомерВычетаЧисло = 0;
	Для каждого ВычетыЭлемент из НалоговыеВычетыСоответсвие Цикл
		НомерВычетаЧисло = НомерВычетаЧисло + 1;
		НаименованиеПоказателей.Параметры["КодВычета"+НомерВычетаЧисло] = ВычетыЭлемент.Значение;
	КонецЦикла;
	ТабДок.Вывести(НаименованиеПоказателей);
	    	
	// Месяцы:
	Для НомерМесяца = 1 по 12 Цикл
	
		МесяцТаблица = Макет.ПолучитьОбласть("инкРаздел3ВычетыЗаИсключениемСтандартных|МесяцТаблица");
		НомерВычетаЧисло = 0;
		
		Для каждого ВычетыЭлемент из НалоговыеВычетыСоответсвие Цикл
		
			НомерВычетаЧисло = НомерВычетаЧисло + 1;
		    ВычетЗаМесяцЧисло = 0;
			
			Для каждого ВычетЗаИсключениемСтандартных из ВычетыЗаИсключениемСтандартных Цикл
				
				Если (НомерМесяца = ВычетЗаИсключениемСтандартных.МесяцНалоговогоПериода) И
					 (ВычетыЭлемент.Значение = ВычетЗаИсключениемСтандартных.КодВычетаСтрокой)
				Тогда
					ВычетЗаМесяцЧисло = ВычетЗаМесяцЧисло + ВычетЗаИсключениемСтандартных.СуммаВычета;
				КонецЕсли;
				
			КонецЦикла;
			
			МесяцТаблица.Параметры["Вычет"+НомерВычетаЧисло] = ВычетЗаМесяцЧисло;

		КонецЦикла;

		ТабДок.Присоединить(МесяцТаблица);

	КонецЦикла;

	// Итоги:
	НомерВычетаЧисло = 0;
	ИтогТаблица = Макет.ПолучитьОбласть("инкРаздел3ВычетыЗаИсключениемСтандартных|ИтогТаблица");
	Для каждого ВычетыЭлемент из НалоговыеВычетыСоответсвие Цикл
		
		НомерВычетаЧисло = НомерВычетаЧисло + 1;
		ИтогПоВычетуЗаГодЧисло = 0;

		Для каждого ВычетЗаИсключениемСтандартных из ВычетыЗаИсключениемСтандартных Цикл
			
			Если (ВычетыЭлемент.Значение = ВычетЗаИсключениемСтандартных.КодВычетаСтрокой) Тогда
				ИтогПоВычетуЗаГодЧисло = ИтогПоВычетуЗаГодЧисло + ВычетЗаИсключениемСтандартных.СуммаВычета;
			КонецЕсли;
			
		КонецЦикла;
		
		ИтогТаблица.Параметры["Вычет"+НомерВычетаЧисло+"Итог"] = ИтогПоВычетуЗаГодЧисло;
		
	КонецЦикла;
	
	ТабДок.Присоединить(ИтогТаблица);
	
КонецПроцедуры

Процедура ВывестиРаздел3_Раздел3ДоходыЗаИсключениемВычетов(ОблагаемыеДоходы,ВычетыЗаИсключениемСтандартных,ТабДок,Макет)
	
	// Наименование:
	НаименованиеПоказателей = Макет.ПолучитьОбласть("инкРаздел3ДоходыЗаИсключениемВычетов|НаименованиеПоказателей");
	ТабДок.Вывести(НаименованиеПоказателей);

	ДоходСНачалаГода = 0;
	Для НомерМесяца = 1 по 12 Цикл
		
		ДоходЧисло = 0;
		
		// Доходы:
		Для каждого ОблагаемыеДоходыЭлемент из ОблагаемыеДоходы Цикл
			
			Если (НомерМесяца = ОблагаемыеДоходыЭлемент.МесяцНалоговогоПериода) 
			Тогда
				ДоходЧисло = ДоходЧисло + ОблагаемыеДоходыЭлемент.СуммаДохода;
			КонецЕсли;
			
		КонецЦикла;
		
		// Вычеты:
		Для каждого ВычетыЭлемент из ВычетыЗаИсключениемСтандартных Цикл
			
			Если (НомерМесяца = ВычетыЭлемент.МесяцНалоговогоПериода)
			Тогда
				ДоходЧисло = ДоходЧисло - ВычетыЭлемент.СуммаВычета;
			КонецЕсли;
			
		КонецЦикла;

		ДоходСНачалаГода = ДоходСНачалаГода + ДоходЧисло;
		
		МесяцТаблица = Макет.ПолучитьОбласть("инкРаздел3ДоходыЗаИсключениемВычетов|МесяцТаблица");
		МесяцТаблица.Параметры.ДоходЗаМесяц     	= ДоходЧисло;
		МесяцТаблица.Параметры.ДоходСНачалаГода		= ДоходСНачалаГода;

		ТабДок.Присоединить(МесяцТаблица);

	КонецЦикла;
	
	//
	ИтогТаблица = Макет.ПолучитьОбласть("инкРаздел3ДоходыЗаИсключениемВычетов|ИтогТаблица");
	ИтогТаблица.Параметры.ДоходЗаМесяцИтог 		= ДоходСНачалаГода;
	ИтогТаблица.Параметры.ДоходСНачалаГодаИтог  = ДоходСНачалаГода;
	ТабДок.Присоединить(ИтогТаблица);
		
КонецПроцедуры

Процедура ВывестиРаздел3_СтандартныеНалоговыеВычеты(СтандартныеНалоговыеВычеты,ТабДок,Макет)

	// Стандартные налоговые вычеты:
	НалоговыеВычетыСоответсвие = Новый Соответствие;
	Для каждого ВычетыЭлемент из СтандартныеНалоговыеВычеты Цикл
		
		Попытка 
			КодВычета = Число(Лев(ВычетыЭлемент.КодВычетаСтрокой.Код,3));
		Исключение
			КодВычета = 0;
		КонецПопытки;
			
		Если (КодВычета >= 103) И (КодВычета <= 105) Тогда
			НалоговыеВычетыСоответсвие.Вставить(ВычетыЭлемент.КодВычетаСтрокой,"Вычет1");	
		ИначеЕсли (КодВычета >= 126) И (КодВычета <= 133) Тогда
			НалоговыеВычетыСоответсвие.Вставить(ВычетыЭлемент.КодВычетаСтрокой,"Вычет2");	
		ИначеЕсли (КодВычета >= 134) И (КодВычета <= 149) Тогда
			НалоговыеВычетыСоответсвие.Вставить(ВычетыЭлемент.КодВычетаСтрокой,"Вычет3");	
		Иначе	
			НалоговыеВычетыСоответсвие.Вставить(ВычетыЭлемент.КодВычетаСтрокой,"Вычет4");	
		КонецЕсли;
		
	КонецЦикла;
	
	// Наименование:
	НаименованиеПоказателей = Макет.ПолучитьОбласть("инкРаздел3СтандартныеНалоговыеВычеты|НаименованиеПоказателей");
	ТабДок.Вывести(НаименованиеПоказателей);
	
	// Месяцы:
	ВычетыСНачалаГодаЧисло = 0;
	Для НомерМесяца = 1 по 12 Цикл
	
		МесяцТаблица = Макет.ПолучитьОбласть("инкРаздел3СтандартныеНалоговыеВычеты|МесяцТаблица");
		ВычетЗаМесяцЧисло = 0;
		
		Для НомерГруппыВычета = 1 по 4 Цикл
			
			ВычетЗаМесяцЧисло = 0;
			Для каждого СтандартныйВычет из СтандартныеНалоговыеВычеты Цикл
				
				Если (НомерМесяца = СтандартныйВычет.МесяцНалоговогоПериода) И
					 (НалоговыеВычетыСоответсвие[СтандартныйВычет.КодВычетаСтрокой] = "Вычет"+НомерГруппыВычета)
				Тогда
					ВычетЗаМесяцЧисло = ВычетЗаМесяцЧисло + СтандартныйВычет.СуммаВычета;
				КонецЕсли;
				
			КонецЦикла;
			
			МесяцТаблица.Параметры["Вычет"+НомерГруппыВычета] = ВычетЗаМесяцЧисло;
			ВычетыСНачалаГодаЧисло = ВычетыСНачалаГодаЧисло + ВычетЗаМесяцЧисло;

		КонецЦикла;
		
		МесяцТаблица.Параметры.ВычетЗаМесяц = ВычетыСНачалаГодаЧисло;
		ТабДок.Присоединить(МесяцТаблица);

	КонецЦикла;

	// Итоги:
	ИтогТаблица = Макет.ПолучитьОбласть("инкРаздел3СтандартныеНалоговыеВычеты|ИтогТаблица");
	
	Для НомерГруппыВычета = 1 по 4 Цикл

		Для каждого ВычетыЭлемент из НалоговыеВычетыСоответсвие Цикл
			
			ИтогПоВычетуЗаГодЧисло = 0;

			Для каждого СтандартныйВычет из СтандартныеНалоговыеВычеты Цикл
				
				Если (НалоговыеВычетыСоответсвие[СтандартныйВычет.КодВычетаСтрокой] = "Вычет"+НомерГруппыВычета) Тогда
					ИтогПоВычетуЗаГодЧисло = ИтогПоВычетуЗаГодЧисло + СтандартныйВычет.СуммаВычета;
				КонецЕсли;
				
			КонецЦикла;
			
			ИтогТаблица.Параметры["Вычет"+НомерГруппыВычета+"Итог"] = ИтогПоВычетуЗаГодЧисло;
			
		КонецЦикла;
		
	КонецЦикла;
	
	ИтогТаблица.Параметры.ВычетыИтог = ВычетыСНачалаГодаЧисло;
	ТабДок.Присоединить(ИтогТаблица);

КонецПроцедуры

Процедура ВывестиРаздел3_ОбщиеИтоги(СотрудникСтрока,ОблагаемыеДоходы,ВычетыЗаИсключениемСтандартных,СтандартныеНалоговыеВычеты,НалогУдержанный,ТабДок,Макет);

	// Наименование:
	НаименованиеПоказателей = Макет.ПолучитьОбласть("инкРаздел3ОбщиеИтоги|НаименованиеПоказателей");
	ТабДок.Вывести(НаименованиеПоказателей);
	
	НалоговаяБазаИтог		= 0;
	НалогИсчисленныйИтог    = 0;
	НалогУдержанныйИтог     = 0;
	ДолгПоНалогуИтог        = 0;
	
	// Месяцы:
	Для НомерМесяца = 1 по 12 Цикл

		НалоговаяБазаМесяцЧисло 	= 0;
		НалогИсчисленныйМесяцЧисло 	= 0;
		НалогУдержанныйМесяцЧисло 	= 0;
		ДолгПоНалогуМесяцЧило 		= 0;
		
		// Доход:		
		ОблагаемыйДоходМесяцЧило 	= 0;
		Для каждого ОблагаемыеДоходыСтрока из ОблагаемыеДоходы Цикл
			
			Если (НомерМесяца = ОблагаемыеДоходыСтрока.МесяцНалоговогоПериода) Тогда
				ОблагаемыйДоходМесяцЧило = ОблагаемыйДоходМесяцЧило + ОблагаемыеДоходыСтрока.СуммаДохода;
			КонецЕсли;
			
		КонецЦикла;
		
		// Вычеты:
		ВычетыМесяцЧисло 			= 0;
		Для каждого ВычетСтрока из ВычетыЗаИсключениемСтандартных Цикл
			
			Если (НомерМесяца = ВычетСтрока.МесяцНалоговогоПериода) Тогда
				ВычетыМесяцЧисло = ВычетыМесяцЧисло + ВычетСтрока.СуммаВычета;
			КонецЕсли;
			
		КонецЦикла;
		
		Для каждого ВычетСтрока из СтандартныеНалоговыеВычеты Цикл
			
			Если (НомерМесяца = ВычетСтрока.МесяцНалоговогоПериода) Тогда
				ВычетыМесяцЧисло = ВычетыМесяцЧисло + ВычетСтрока.СуммаВычета;
			КонецЕсли;
			
		КонецЦикла;
		
		// Налоговая база:
		НалоговаяБазаМесяцЧисло = ОблагаемыйДоходМесяцЧило - ВычетыМесяцЧисло;
		Если НалоговаяБазаМесяцЧисло < 0 Тогда                    
			НалоговаяБазаМесяцЧисло = 0;
		КонецЕсли;
		
		// Налог исчисленный:
		ПроцентНалога = инкНалогСервер.ПолучитьПроцентНалогаСотрудника(СотрудникСтрока);
		НалоговаяСтавка = инкНалогСервер.ПолучитьНалоговуюСтавкуСотрудника(СотрудникСтрока); 
		НалогИсчисленныйМесяцЧисло = Окр(НалоговаяБазаМесяцЧисло * ПроцентНалога);
		
		// Налог удержанный:
		Для каждого НалогУдержанныйСтрока из НалогУдержанный Цикл
			
			Если (НомерМесяца = НалогУдержанныйСтрока.МесяцНалоговогоПериода) И
				 (НалоговаяСтавка = НалогУдержанныйСтрока.НалоговаяСтавка)
			Тогда
				НалогУдержанныйМесяцЧисло = НалогУдержанныйМесяцЧисло + НалогУдержанныйСтрока.СуммаНалога;
			КонецЕсли;
			
		КонецЦикла;
		
		НаименованиеПоказателей = Макет.ПолучитьОбласть("инкРаздел3ОбщиеИтоги|МесяцТаблица");
		
		НалоговаяБазаИтог		= НалоговаяБазаИтог + НалоговаяБазаМесяцЧисло;
		НалогИсчисленныйИтог    = НалогИсчисленныйИтог + НалогИсчисленныйМесяцЧисло;
		НалогУдержанныйИтог     = НалогУдержанныйИтог + НалогУдержанныйМесяцЧисло;
		ДолгПоНалогуИтог        = ДолгПоНалогуИтог + (НалогИсчисленныйМесяцЧисло - НалогУдержанныйМесяцЧисло);
		
		НаименованиеПоказателей.Параметры.НалоговаяБаза		= НалоговаяБазаМесяцЧисло;
		НаименованиеПоказателей.Параметры.НалоговаяБазаСНачалаГода = НалоговаяБазаИтог;
		НаименованиеПоказателей.Параметры.НалогИсчисленный  = НалогИсчисленныйМесяцЧисло; 
		НаименованиеПоказателей.Параметры.НалогУдержанный   = НалогУдержанныйМесяцЧисло;
		НаименованиеПоказателей.Параметры.ДолгПоНалогу      = НалогИсчисленныйМесяцЧисло - НалогУдержанныйМесяцЧисло;
		
		ТабДок.Присоединить(НаименованиеПоказателей);
		
	КонецЦикла;
	
	// Итоги:
	ИтогТаблица = Макет.ПолучитьОбласть("инкРаздел3ОбщиеИтоги|ИтогТаблица");
	ИтогТаблица.Параметры.НалоговаяБазаИтог 	= НалоговаяБазаИтог;
	ИтогТаблица.Параметры.НалоговаяБазаСНачалаГодаИтог	= НалоговаяБазаИтог; 
	ИтогТаблица.Параметры.НалогИсчисленныйИтог 	= НалогИсчисленныйИтог;
	ИтогТаблица.Параметры.НалогУдержанныйИтог 	= НалогУдержанныйИтог;
	ИтогТаблица.Параметры.ДолгПоНалогуИтог 		= ДолгПоНалогуИтог;
	
	ТабДок.Присоединить(ИтогТаблица);

КонецПроцедуры

Процедура ВывестиРаздел3_ПлатежноеПоручение(ТабДок,Макет)
	
	// Заголовок:
	НаименованиеПоказателей = Макет.ПолучитьОбласть("инкРаздел3ПлатежноеПоручение|НаименованиеПоказателей");
  	ТабДок.Вывести(НаименованиеПоказателей);
	//
	Для НомерМесяца = 1 по 12 Цикл
		
		МесяцТаблица = Макет.ПолучитьОбласть("инкРаздел3ПлатежноеПоручение|МесяцТаблица");
  		ТабДок.Присоединить(МесяцТаблица);

	КонецЦикла;
	//
	ИтогТаблица = Макет.ПолучитьОбласть("инкРаздел3ПлатежноеПоручение|ИтогТаблица");
  	ТабДок.Присоединить(ИтогТаблица);
	
КонецПроцедуры

Процедура ВывестиРаздел3_Подвал(ТабДок,Макет)

	НаименованиеПоказателей = Макет.ПолучитьОбласть("инкРаздел3Подвал");
  	ТабДок.Вывести(НаименованиеПоказателей);
	
КонецПроцедуры		

#КонецОбласти 

