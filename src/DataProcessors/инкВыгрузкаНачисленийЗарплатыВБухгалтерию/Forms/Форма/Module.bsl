
&НаКлиенте
Процедура ПриОткрытии(Отказ)
    
    Объект.ПериодВыгрузки.ДатаНачала    = НачалоГода(ТекущаяДата());
    Объект.ПериодВыгрузки.ДатаОкончания = КонецГода(ТекущаяДата());
    
КонецПроцедуры

&НаКлиенте
Процедура ВыгрузитьДанные(Команда)
    
    ФайлВыгрузкиАдрес = ВыгрузитьДанныеНаСервере();
    
    Если ФайлВыгрузкиАдрес = Неопределено Тогда
    	Возврат;
    КонецЕсли;                                          

	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("ФайлВыгрузкиАдрес",ФайлВыгрузкиАдрес);
	ДополнительныеПараметры.Вставить("ИмяФайла","Файл выгрузки начислений ИНКОМСОФТ.xml");
	ДополнительныеПараметры.Вставить("ВидКлиента");
	
	#Если ВебКлиент Тогда     
		
		ДополнительныеПараметры.ВидКлиента = "ВебКлиент";	
		
		списокКнопокВопроса = новый СписокЗначений();
		списокКнопокВопроса.Добавить("Сохранить","Сохранить");
		списокКнопокВопроса.Добавить("Отмена","Отмена");
		
		Оповещение = новый ОписаниеОповещения("СохранитьФайлЗавершение",ЭтотОбъект,ДополнительныеПараметры);
	  	ПоказатьВопрос(Оповещение,"Выберите действие",списокКнопокВопроса,30,"Сохранить","Сохранить файл выгрузки?","Отмена");

	#Иначе	
		
		ДополнительныеПараметры.ВидКлиента = "";	

		//выбор файла для открытия	
		Режим = РежимДиалогаВыбораФайла.Сохранение;
		Диалог = Новый ДиалогВыбораФайла(Режим);
		Диалог.ПолноеИмяФайла = ДополнительныеПараметры.ИмяФайла;
		
		ОбратныйВызов = Новый ОписаниеОповещения("СохранитьФайлЗавершение", ЭтотОбъект, ДополнительныеПараметры); 
		//открывает диалоговое окно
		Диалог.Показать(ОбратныйВызов);
		
	#КонецЕсли       

КонецПроцедуры
   
&НаКлиенте
Процедура СохранитьФайлЗавершение(РезультатВопроса,ДополнительныеПараметры) экспорт
	
	Если РезультатВопроса = Неопределено Тогда
		Возврат;	
	КонецЕсли;
	
	ФайлВыгрузкиАдрес = ДополнительныеПараметры.ФайлВыгрузкиАдрес;
	Если ДополнительныеПараметры.ВидКлиента = "ВебКлиент" Тогда

		Если РезультатВопроса = "Сохранить" тогда
			НачатьПолучениеФайлаССервера(ФайлВыгрузкиАдрес,ДополнительныеПараметры.ИмяФайла);
		Иначе
			Возврат;
		КонецЕсли;		

	Иначе                              
		ПутьКФайлу = РезультатВопроса[0];
		НачатьПолучениеФайлаССервера(,ФайлВыгрузкиАдрес, ПутьКФайлу);	
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция ВыгрузитьДанныеНаСервере()
    
    АдресФайла = Неопределено;
    
    Попытка
        
        ВремФайл = ПолучитьИмяВременногоФайла("xml");
        МакетПравилОбмена = ПолучитьМакетНаСервере("ПравилаВыгрузкиДанных");
        МакетПравилОбмена.Записать(ВремФайл);
        
        ИмяФайлаВыгрузки = ПолучитьИмяВременногоФайла("xml");
        
        Обработка = Обработки.УниверсальныйОбменДаннымиXML.Создать();
        Обработка.РежимОбмена="Выгрузка";
        Обработка.ИмяФайлаПравилОбмена = ВремФайл; 
        Обработка.ЗагружатьДанныеВРежимеОбмена = Истина;
        Обработка.ЗаписыватьРегистрыНаборамиЗаписей = Истина;
        Обработка.ЗапоминатьЗагруженныеОбъекты = Истина;
        Обработка.ИспользоватьОтборПоДатеДляВсехОбъектов = Истина;
        Обработка.ВыгружатьТолькоРазрешенные = Истина;
        Обработка.ИмяФайлаОбмена = ИмяФайлаВыгрузки;
        Обработка.ТипУдаленияРегистрацииИзмененийДляУзловОбменаПослеВыгрузки = 0; // 0 - не снимать регистрацию, 1 - снимать регистрацию
        Обработка.ЗагрузитьПравилаОбмена();
        //
        Обработка.Параметры.Дата1 = Объект.ПериодВыгрузки.ДатаНачала;
        Обработка.Параметры.Дата2 = Объект.ПериодВыгрузки.ДатаОкончания;
        //
        Обработка.ВыполнитьВыгрузку();    
        
        АдресФайла = ПоместитьВоВременноеХранилище(новый ДвоичныеДанные(ИмяФайлаВыгрузки));

    Исключение
        Сообщить("Ошибка при формировании файла выгрузки. Описание: "+ОписаниеОшибки());
    КонецПопытки; 
    
    Возврат АдресФайла;    
    
КонецФункции

&НаСервере
Функция ПолучитьМакетНаСервере(ИмяМакета) 
    
	Возврат РеквизитФормыВЗначение("Объект").ПолучитьМакет(ИмяМакета);
    
КонецФункции
